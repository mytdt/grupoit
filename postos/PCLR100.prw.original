#INCLUDE "PCLR100.CH"
#INCLUDE "RWMAKE.CH"
#INCLUDE "PROTHEUS.CH"


/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³PCLR100   º Autor ³ Welinton Fernandes º Data ³  13/06/01   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescricao ³ Relatorio que gera o LMC (Livro de Movimentacao de 		  º±±
±±º          ³ Combustivel)												  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ SIGALOJA                                                   º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

User Function PCLR100()

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Declaracao de Variaveis                                             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

Private cString      := ""
Private aOrd         := {}
Private cDesc1       := STR0001 //"Este programa tem como objetivo imprimir relatorio "
Private cDesc2       := STR0002 //"de acordo com os parametros informados pelo usuario."
Private cDesc3       := STR0003 //"Livro de Movimentacao de Combustivel"
Private cPict        := ""
Private lEnd         := .F.
Private lAbortPrint  := .F.
Private limite       := 80
Private tamanho      := "P"
Private nomeprog     := "PCLR100" // Coloque aqui o nome do programa para impressao no cabecalho
Private nTipo        := 18
Private aReturn      := { "Zebrado", 1, "Administracao", 2, 2, 1, "", 1}
Private nLastKey     := 0
Private cPerg        := "PCLR100"
Private titulo       := STR0003 //"Livro de Movimentacao de Combustivel"
Private nLin         := 80
Private Cabec1       := ""
Private Cabec2       := ""
Private cbtxt        := Space(10)
Private cbcont       := 00
Private CONTFL       := 01
Private m_pag        := 01
Private imprime      := .T.
Private wnrel        := "PCLR100" // Coloque aqui o nome do arquivo usado para impressao em disco

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Declaracao de Variaveis contadoras			                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

Private cFOLHA  := Space(3)
Private nPERGAN := nPREPRO := nREC    := nAFERIC :=0
Private nTANQUE := nESTI := nPRV := 0
Private nVTAQ01 := nVTAQ02 := nVTAQ03 := nVTAQ04 := nVTAQ05 := 0
Private nVTAQ06 := nVTAQ07 := nVTAQ08 := nVTAQ09 := nVTAQ10 := 0
Private nVTAQ11 := nVTAQ12 := nVTAQ13 := nVTAQ14 := nVTAQ15 := 0
Private nVTAQ16 := nVTAQ17 := nVTAQ18 := nVTAQ19 := nVTAQ20 := 0
Private nESTI01 := nESTI02 := nESTI03 := nESTI04 := nESTI05 := 0
Private nESTI06 := nESTI07 := nESTI08 := nESTI09 := nESTI10 := 0
Private nESTI11 := nESTI12 := nESTI13 := nESTI14 := nESTI15 := 0
Private nESTI16 := nESTI17 := nESTI18 := nESTI19 := nESTI20 := 0

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Validacao a Autorizacao de Uso de Template                          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
// cGrupo, cOrdem, cPergunt, cPerSpa, cPerEng, cVar, cTipo ,nTamanho, nDecimal, nPresel, cGSC, cValid, cF3, cGrpSxg, cPyme, cVar01, cDef01, cDefSpa1, cDefEng1, cCnt01, cDef02, cDefSpa2, cDefEng2, cDef03, cDefSpa3, cDefEng3, cDef04, cDefSpa4, cDefEng4, cDef05, cDefSpa5, cDefEng5, aHelpPor, aHelpEng, aHelpSpa, cHelp
PutSx1(cPerg, "01", "Data do Livro de ", "", "", "mv_ch1", "D", 08, 00, 00, "G", "", "   ", "", "", "MV_PAR01", "    ", "", "", "", "    ", "", "", "", "", "", "", "", "", "", "", "", {}, {}, {}, "")
PutSx1(cPerg, "02", "Data do Livro ate", "", "", "mv_ch2", "D", 08, 00, 00, "G", "", "   ", "", "", "MV_PAR02", "    ", "", "", "", "    ", "", "", "", "", "", "", "", "", "", "", "", {}, {}, {}, "")
PutSx1(cPerg, "03", "Do Produto       ", "", "", "mv_ch3", "C", 15, 00, 00, "G", "", "SB1", "", "", "MV_PAR03", "    ", "", "", "", "    ", "", "", "", "", "", "", "", "", "", "", "", {}, {}, {}, "")
PutSx1(cPerg, "04", "Folha            ", "", "", "mv_ch4", "C", 05, 00, 00, "G", "", "   ", "", "", "MV_PAR04", "    ", "", "", "", "    ", "", "", "", "", "", "", "", "", "", "", "", {}, {}, {}, "")

If !Pergunte(cPerg,.T.)
	Return()
EndIf

dbSelectArea("LEF")
dbSetOrder(1)
dbGoTop()

wnrel := SetPrint(cString,NomeProg,cPerg,@titulo,cDesc1,cDesc2,cDesc3,.F.,aOrd,.T.,Tamanho,,.F.)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Monta a interface padrao com o usuario...                           ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If nLastKey == 27
	Return
Endif

SetDefault(aReturn,cString,,,Tamanho,1)

If nLastKey == 27
	Return
Endif

nTipo := If(aReturn[4]==1,15,18)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Processamento. RPTSTATUS monta janela com a regua de processamento. ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

RptStatus({|| RunReport(Cabec1,Cabec2,Titulo,nLin) },Titulo)
Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFun??o    ³RUNREPORT º Autor ³ Welinton           º Data ³  13/06/01   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescri??o ³ Funcao auxiliar chamada pela RPTSTATUS. A funcao RPTSTATUS º±±
±±º          ³ monta a janela com a regua de processamento.               º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ Programa principal                                         º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

Static Function RunReport(Cabec1,Cabec2,Titulo,nLin)

Local aColsEst   := {}
Local aColsVen   := {}
Local nVTAQ      := 0
Local cTQ        := ""
Local cTanque    := ""
Local nCol       := 0
Local nI         := 0
Local nPos       := 0
Local n          := 0
Local __nTanque  := 0
Local nColuna	  := 0
Local nC		:= 0

Local cArquivo
Local cChave
Local cFor
Local nIndex    
Local cAno := ""
Local cMes := ""
Local aStruct	:= LEF->(dbStruct())
Local aLEF		:= {}

SetRegua(DateDiffDay(MV_PAR01,MV_PAR02))

While MV_PAR01 <= MV_PAR02
	
	dbSelectArea("SD1")
	dbSetOrder(7) //D1_FILIAL+D1_COD+D1_LOCAL+DTOS(D1_DTDIGIT)+D1_NUMSEQ
	
	dbSelectArea("SB1")
	dbSetOrder(1) //B1_FILIAL+B1_COD
	
	SB1->(dbSeek(xFilial("SB1")+MV_PAR03))
	
	aPergs	:= {}
	aRet	:= {}
	cObs	:= Space(TamSX3("LMC_OBS")[1])
	dDataAux:= MV_PAR01
	dbSelectArea("LMC")
	dbSetOrder(1)
	
	lAppend := !LMC->(dbSeek(xFilial("LMC")+SB1->B1_COD+Dtos(MV_PAR01)))
	
	If !lAppend
		cObs := LMC->LMC_OBS
	EndIf
	
	aAdd( aPergs ,{1,"Observação : ",cObs,"@!",'.T.',,'.T.',80,.F.})
	
	If ParamBox(aPergs ,"Parametros ",aRet)
		
		cObs := aRet[1]
		RecLock("LMC",lAppend)
		
		If lAppend
			LMC->LMC_FILIAL := xFilial("LMC")
			LMC->LMC_PROD 	:= SB1->B1_COD
			LMC->LMC_DATA	:= dDataAux
		EndIf
		
		LMC->LMC_OBS	:= cObs
		MsUnLock()
		
	EndIf
	
	cObs := PADR(cObs,344)
	
	MV_PAR01 := dDataAux
	
	dbSelectArea("LEI")
	
	cFiltro := "LEI_FILIAL == '"+xFilial("LEI")+"'"
	cFiltro	+= ".And.LEI_PROD == '"+MV_PAR03+"'"
	
	LEI->(DbSetFilter({||&cFiltro}, cFiltro))
	LEI->(dbGoTop())
	
	nSldAber	:= 0
	nSldFecha	:= 0
	nQtdTotal	:= 0
	nCol		:= 0
	nColuna		:= 0
	aSaldo		:= {}
	aColsEst 	:= {}
	aTanques	:= {}
	
	Do While LEI->(!EOF())
		
		nPos := aScan(aTanques,{|x|x[2]==LEI->LEI_TANQUE})
		
		If nPos == 0
			aAdd(aTanques,{LEI->LEI_PROD,LEI->LEI_TANQUE})
		EndIf
		
		LEI->(dbSkip())
	EndDo
	
	For nC := 1 to Len(aTanques)
		
		aSaldo 		:= CalcEst(aTanques[nC][1],aTanques[nC][2],MV_PAR01)
		nSaldoIni 	:= aSaldo[1]
		nSldAber	+= nSaldoIni
		
		aSaldo 	 	:= CalcEst(aTanques[nC][1],aTanques[nC][2],MV_PAR01+1)
		nSaldoFim	:= aSaldo[1]
		
		nSldFecha	+= nSaldoFim
		
		Aadd(aColsEst,{"|TQ-"+aTanques[nC][2],nSaldoIni,nCol,nSaldoFim,aTanques[nC][1],aTanques[nC][2]} )
		nCol += 11
		
	Next nC
	
	dbSelectArea("SM0")
	dbSeek(cEmpAnt+cFilAnt)
	
	cFOLHA := Val(MV_PAR04)
	nREC := Recno()
	
	Cabec1 := Alltrim(SUBSTR(SM0->M0_NOMECOM,1,35)) + " - " + Alltrim(SM0->M0_FILIAL)+ " / CNPJ "+Transform(SM0->M0_CGC,"@R 99.999.999/9999-99")
	
	If nLin > 55
		Cabec(Titulo,Cabec1,Cabec2,NomeProg,Tamanho,nTipo)
		nLin := 8
	EndIf
	
	@ Prow()+1,001 Psay "+-----------------------------------------------------------------------------+"
	
	@ Prow()+1,001 Psay STR0008 + STR0009 + PadR(DTOC(MV_PAR01),10)+"|"
	
	cDESC := Alltrim(SB1->B1_DESC)
	
	@ Prow()+1,001 Psay STR0010 + Alltrim(MV_PAR03) + " - " + cDESC
	@ Prow(),64    Psay STR0011 + Strzero(cFOLHA,5,0) + "|" //"| [1] Produto: "###" Folha No:"
	
	@ Prow()+1,001 Psay "|-----------------------------------------------------------------------------|"
	
	@ Prow()+1,001 Psay "|       [3] Estoque de Abertura (Medicao Fisica no Inicio do Dia)             |"
	
	@ Prow()+1,001 Psay "|-----------------------------------------------------------------------------|"
	// 01234567890123456789012345678901234567890123456789012345678901234567890123456789
	// |TQ-01     |TQ-01     |TQ-01     |TQ-01     |TQ-01     |[3.1]Estoque Abertura |
	// |999999.999|999999.999|999999.999|999999.999|999999.999|      999,999,999.999 |
	@ Prow()+1,000 Psay ""
	
	For nI := 1 To Len(aColsEst)
		nColuna := (aColsEst[nI][3]+1)
		@ Prow(), nColuna Psay aColsEst[nI][1]
	Next
	
	@ Prow(), nColuna+12  Psay "|"
	@ Prow(),57 Psay "|[3.1]Estoque Abertura|"
	@ Prow()+1,000 Psay ""
	
	For nI := 1 To Len(aColsEst)
		nColuna := (aColsEst[nI][3]+1)
		@ Prow(), nColuna Psay "|"
		@ Prow(), nColuna+1 Psay aColsEst[nI][2] Picture "@E 999999.999"
	Next
	
	@ Prow(), nColuna+12  Psay "|"
	@ Prow(),057 Psay "|"
	@ Prow(),064 Psay nSldAber Picture "@E 999,999,999.999" + "|"
	
	@ Prow()+1,001 Psay "|-----------------------------------------------------------------------------|"
	
	@ Prow()+1,001 Psay STR0014 //"|[4] Volume Recebido no Dia (Em Litros) |[4.1] No TQ Desc.|[4.2] Volume Rece. |"
	
	@ Prow()+1,001 Psay "|-----------------------------------------------------------------------------|"
	
	For nC := 1 To Len(aColsEst)
		If SD1->(MsSeek(xFilial("SD1")+aColsEst[nC][5]+aColsEst[nC][6]+DTOS(MV_PAR01)))
			While SD1->(!EOF() .And. D1_FILIAL+D1_COD+D1_LOCAL+DTOS(D1_DTDIGIT) == xFilial("SD1")+aColsEst[nC][5]+aColsEst[nC][6]+DTOS(MV_PAR01))
				
				If SD1->D1_TIPO != 'D'
					
					nQtdTotal	+= SD1->D1_QUANT
					
					@ Prow()+1,001 Psay STR0015 + SD1->D1_DOC  + " de " + DTOC(MV_PAR01)+ Space(4) + "| " + SD1->D1_LOCAL
					@ Prow(),059 Psay "|"
					@ Prow(),064 Psay SD1->D1_QUANT Picture "@E 999,999,999.999" + "|"
					
				EndIf
				
				SD1->(dbSkip())
			EndDo
		EndIf
	Next nC
	
	If nQtdTotal = 0
		@ Prow()+1,001 Psay STR0015 + Space(10) + " de " + Space(08) + "| " + Space(16) + "|" + Space(19) + "|" //"| Nota Fiscal Nr. "
	Endif
	
	@ Prow()+1,001 Psay "|-----------------------------------------------------------------------------|"
	
	@ Prow()+1,001 Psay STR0016 //"|                                    [4.3] Total Recebido |"
	@ Prow(),060 Psay nQtdTotal Picture "@E 999,999,999,999.999" + "|"
	
	nVolDisp	:= nSldAber+nQtdTotal
	
	@ Prow()+1,001 Psay STR0017 //"|                   [4.4] Volume Disponivel (3.1) + (4.3) |"
	@ Prow(),060 Psay nVolDisp Picture "@E 999,999,999,999.999" + "|"
	
	@ Prow()+1,001 Psay "|-----------------------------------------------------------------------------|"
	
	@ Prow()+1,001 Psay STR0018 //"|              [5] Volume Vendido no Dia ( Em Litros )                        |"
	
	@ Prow()+1,001 Psay "|-----------------------------------------------------------------------------|"
	
	@ Prow()+1,001 Psay STR0019 //"|[5.1] TQ|[5.2] Bico|[5.3] + Fech.|[5.4] - Abert.|[5.5]-Afr.|[5.6]=Vendas Bico|"
	
	@ Prow()+1,001 Psay "|-----------------------------------------------------------------------------|"
	
	
	cMes := Strzero(Month(MV_PAR02),2)
	cAno := Strzero(Year(MV_PAR02),2)
	
	cSql := " SELECT SUM(L2_VLRITEM) VALOR "
	cSql += " FROM "+RetSqlName("SL2")+" SL2, "+RetSqlName("SL1")+" SL1 "
	cSql += " WHERE L2_FILIAL = '"+ xFilial("SL2") +"' "
	cSql += " AND   L1_NUM = L2_NUM "
	cSql += " AND   L2_EMISSAO BETWEEN '"+DTOS(MV_PAR01)+"' AND '"+DTOS(MV_PAR02)+"' "
	//cSql += " AND   SUBSTRING(L2_EMISSAO,1,4) = '"+cAno+"' "
	//cSql += " AND   SUBSTRING(L2_EMISSAO,1,4) = '"+cAno+"' "	
	cSql += " AND   L2_PRODUTO = '"+ALLTRIM(MV_PAR03)+"' "
	cSql += " AND   L1_TAFERIC <> 'S' "
	cSql += " AND   L1_STORC <> 'C' "	
	cSql += " AND   SL2.D_E_L_E_T_ <> '*' "
	cSql += " AND   SL1.D_E_L_E_T_ <> '*' "
	
	If Select("TRB") > 0
		TRB->(dbCloseArea())
	EndIf
	
	dbUseArea(.T.,"TOPCONN",tcGenQry(,,cSql),"TRB",.F.,.T.)
	
	nVlrTotal	:= TRB->VALOR
	
	TRB->(dbCloseArea())
	
	
	dbSelectArea("SL1")
	dbSetOrder(1) //L1_FILIAL+L1_NUM
	
	dbSelectArea("SL2")
	dbSetOrder(1) //L2_FILIAL+L2_NUM+L2_ITEM+L2_PRODUTO
	
	cFiltro := "L1_FILIAL == '"+xFilial("SL1")+"' "
	cFiltro	+= ".And.Dtos(L1_EMISNF) >= '"+Dtos(mv_par01)+"' "
	cFiltro	+= ".And.Dtos(L1_EMISNF) <= '"+Dtos(mv_par02)+"'"
	
	SL1->(DbSetFilter({||&cFiltro}, cFiltro))
	SL1->(dbGoTop())
	
	aColsVen := {}
	
	While SL1->(!EOF())
		
		SL2->(dbSeek(xFilial("SL2")+SL1->L1_NUM))
		
		While SL2->(!EOF() .And. L2_FILIAL+L2_NUM == xFilial("SL2")+SL1->L1_NUM)
			
			If !Empty(SL2->L2_TBICO) .And. SL2->L2_VENDIDO == "S" .And. Alltrim(SL2->L2_PRODUTO)  == Alltrim(MV_PAR03)
				
				nPos 	:= Ascan(aColsVen,{|x| x[1]+x[2] == SL2->L2_LOCAL + SL2->L2_TBICO})
				
				nEncIni := SL2->L2_TENCINI
				nEncFin := SL2->L2_TENCFIM
				cLocal	:= SL2->L2_LOCAL
				cBico	:= SL2->L2_TBICO
				
				If SL1->L1_TAFERIC == "S"
					nAferic := SL2->L2_QUANT
					nQuant  := 0
					nVlrItem:= 0
				Else
					nAferic := 0
					nQuant  := SL2->L2_QUANT
					nVlrItem:= SL2->L2_VLRITEM
				Endif
				
				If nPos > 0
					
					aColsVen[nPos,3] +=  nAferic
					aColsVen[nPos,4] +=  nQuant
					aColsVen[nPos,5] +=  nVlrItem
					
					If aColsVen[nPos][6] > SL2->L2_TENCINI
						aColsVen[nPos][6] := SL2->L2_TENCINI
					EndIf
					
					If aColsVen[nPos][7] < SL2->L2_TENCFIM
						aColsVen[nPos][7] := SL2->L2_TENCFIM
					EndIf
					
				Else
					Aadd(aColsVen,{cLocal,cBico,nAferic,nQuant,nVlrItem,nEncIni,nEncFin})
				Endif
				
			EndIf
			SL2->(dbSkip())
		EndDo
		
		SL1->(dbSkip())
	EndDo
	
	SL1->(dbClearFilter())
	
	aSort(aColsVen,,,{|x,y| x[1]+x[2] < y[1]+y[2]})
	
	dbSelectArea("LEF")
	LEF->(DbSetOrder(3)) //LEF_FILIAL+DTOS(LEF_DATA)+LEF_BICO+LEF_TANQUE+LEF_CODIGO
	
	nQtdTotal := 0
	//nVlrTotal := 0
	nQtdDia  := 0
	nQtdAfer := 0
	nVlrDia  := 0
	
	For n:= 1 To Len(aColsVen)
		
		@ Prow()+1,001 Psay "| " + aColsVen[n][1] + "     | " + aColsVen[n][2] + "       |"
		@ Prow(),022 Psay aColsVen[n][7] Picture "@E 9,999,999,999" + "|" //Fechamento
		@ Prow(),037 Psay aColsVen[n][6] Picture "@E 9,999,999,999" + "|" //Abertura
		@ Prow(),052 Psay aColsVen[n][3] Picture "@E 9,999.999" + "|"
		
		nVolVend := aColsVen[n][7]-aColsVen[n][6]-aColsVen[n][3]
		
		@ Prow(),063 Psay nVolVend Picture "@E 9999,999,999.999" + "|"
		
		nQtdDia  += aColsVen[n][4]
		nVlrDia  += aColsVen[n][5] 
		nQtdAfer += aColsVen[n][3]
				
	Next n
	
	nQtdDia := nQtdDia - nQtdAfer
		
	//nVlrTotal += aColsVen[n][5]
	
	@ Prow()+1,001 Psay "|-----------------------------------------------------------------------------|"
	@ Prow()+1,001 Psay STR0020 //"|[10] Valor das Vendas (R$)                 |[5.7] Vendas no Dia"
	@ Prow(),065 Psay nQtdDia	Picture "@E 99,999,999.999" + "|"
	
	
	@ Prow()+1,001 Psay STR0021 //"|Vlr.Ven.Dia(5.7 X Prc.Bomba)"
	@ Prow(),030 Psay nVlrDia Picture "@E 999,999,999.999" + "|"
	
	@ Prow(),046 Psay STR0022 //"[6]Est.Escrit.(4.4-5.7)"
	@ Prow(),069 Psay nVolDisp-nQtdDia  Picture "@E 999999.999" + "|"
	
	nPERGAN := (nSldFecha - (nVolDisp-nQtdDia) )  // Variavel que armazena: Valores de Perdas e Ganhos
	
	@ Prow()+1,001 Psay "|-----------------------------------------------------------------------------|"
	
	@ Prow()+1,001 Psay STR0023 //"|Vlr.Acumulado no Mes         "
	@ Prow(),031 Psay nVlrTotal Picture "@E 99,999,999.999" + "|"
	
	@ Prow(),046 Psay STR0024 //"[7]Est.Fechamento"
	@ Prow(),063 Psay nSldFecha  Picture "@E 999999999999.999" + "|"
	
	@ Prow()+1,001 Psay "|-------------------------------------------|---------------------------------|"
	
	@ Prow()+1,001 Psay STR0025 //"|[11] Para Uso do Revendedor                |[8] - Perdas                     |"
	@ Prow()+1,001 Psay STR0026 //"|                                           |    + Ganhos (*)"
	@ Prow(),063 Psay nPERGAN Picture "@E 999999999999.999" + "|"
	
	@ Prow()+1,001 Psay "|-------------------------------------------|---------------------------------|"
	
	@ Prow()+1,001 Psay STR0027 //"|[13] Observacoes                           |[12]Destinado a Fiscalizacao     |"
	
	@ Prow()+1,001 Psay "|" + SubsTr(cObs,001,43) + "|---------------------------------|"
	
	@ Prow()+1,001 Psay "|" + SubsTr(cObs,044,43) + "|ANP "+ Space(29) + "|"
	
	@ Prow()+1,001 Psay "|" + SubsTr(cObs,087,43) + "|" + Space(33)+ "|"
	
	@ Prow()+1,001 Psay "|" + SubsTr(cObs,130,43) + "|" + Space(33)+ "|"
	
	@ Prow()+1,001 Psay "|" + SubsTr(cObs,173,43) + "|---------------------------------|"
	
	@ Prow()+1,001 Psay "|"	+ SubsTr(cObs,216,43) + STR0028 //"|OUTROS ORGAOS FISCAIS            |"
	
	@ Prow()+1,001 Psay "|" + SubsTr(cObs,259,43) + "|" + Space(33)+ "|"
	
	@ Prow()+1,001 Psay "|" + SubsTr(cObs,302,43) + "|" + Space(33)+ "|"
	
	@ Prow()+1,001 Psay "|-----------------------------------------------------------------------------|"
	
	@ Prow()+1,001 Psay STR0029 //"|                          Conciliacao dos Estoques                           |"
	
	@ Prow()+1,001 Psay "|-----------------------------------------------------------------------------|"
	
	@ Prow()+1,000 Psay ""
	
	For nI := 1 To Len(aColsEst)
		nColuna := aColsEst[nI][3]
		@ Prow(), nColuna+1 Psay aColsEst[nI][1]
	Next
	@ Prow(), nColuna+12  Psay "|"
	
	@ Prow(),57 Psay "|                     |"
	@ Prow()+1,000 Psay ""
	
	nQtdFinal	:= 0
	
	For nI := 1 To Len(aColsEst)
		nColuna := (aColsEst[nI][3]+1)
		@ Prow(), nColuna   Psay "|"
		@ Prow(), nColuna+1 Psay aColsEst[nI][4] Picture "@E 999999.999"
		nQtdFinal	+= aColsEst[nI][4]
	Next
	
	@ Prow(), nColuna+11  Psay "|"
	@ Prow(),57 Psay "|     T O T A L       |"
	
	// 01234567890123456789012345678901234567890123456789012345678901234567890123456789
	// |TQ-01     |TQ-01     |TQ-01     |TQ-01     |TQ-01     |     T O T A L        |
	// |999999.999|999999.999|999999.999|999999.999|999999.999|[9.1] 999,999,999.999	|
	
	@ Prow()+1,001 Psay "|-------------------------------------------------------|                     |"
	@ Prow()+1,001 Psay "|[9] Fechamento|----------------------------------------|[9.1]"
	
	@ Prow(),064 Psay nSldFecha Picture "@E 999,999,999.999" + "|"
	
	@ Prow()+1,001 Psay STR0031 //"|     Fisico   |--------------------------------------------------------------|"
	
	@ Prow()+1,001 Psay "|-----------------------------------------------------------------------------|"
	
	@ Prow()+1,001 Psay STR0032 //"|(*ATENCAO)-Se o resultado for negativo, pode estar havendo vazamento de      |"
	
	@ Prow()+1,001 Psay STR0033 //"|                       produto para o meio ambiente                          |"
	
	@ Prow()+1,001 Psay "+-----------------------------------------------------------------------------+"
	
	IncRegua()
	
	MV_PAR01 += 1
	MV_PAR04 := Soma1(MV_PAR04)
	nLin := 80
EndDo

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Finaliza a execucao do relatorio...                                 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

SET DEVICE TO SCREEN

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Se impressao em disco, chama o gerenciador de impressao...          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

If aReturn[5]==1
	dbCommitAll()
	SET PRINTER TO
	OurSpool(wnrel)
Endif

MS_FLUSH()

Return
